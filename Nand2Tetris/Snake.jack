class Snake {

    // o jogo the snake está jogando no
    field SnakeGame game;

    // a posição xy da cabeça da cobra
    field int posX, posY;

    // o comprimento da cobra
    field int length;

    // quanto a cobra cresce ao comer uma bola
    field int growby;

    // quanto a cobra ainda consegue crescer
    field int togrow;

    // a direção que a cobra está/estava se movimentando
    // 0=none,1=up,2=down,3=left,4=right
    field int dir, lastdir;

    // historico dos movimentos da cobra
    field Array histX, histY;
    field int histSize;

    // cria uma nova cobra
    constructor Snake new(SnakeGame Agame, int Ax, int Ay, int Alen, int Agrowby) {

        var int i;

        let game = Agame;
        let posX = Ax - Alen;
        let posY = Ay;
        let length = Alen;
        let growby = Agrowby;

        let togrow = 0;
        let dir = 4;
        let lastdir = 4;
        let histSize = 512;
        let histX = Array.new(histSize);
        let histY = Array.new(histSize);

        // desenhando a cobra inicial
        let i = 0;
        while( i < length ) {
            let posX = posX + 1;
            do drawHead(game.getCycle());
            let i = i + 1;
            do game.nextCycle();
        }

        return this;

    }

    // descarta
    method void dispose() {

        do histX.dispose();
        do histY.dispose();
        do Memory.deAlloc(this);
        return;

    }

    // verifica se o historico da cobra precisa ser reescrito
    method boolean checkRewriteHistory() {

        if( game.getCycle() = histSize ) {
            do rewriteHistory();
            return true;
        }

        return false;

    }

    // reescreve o hisotrico da cobra
    method void rewriteHistory() {

        var int i, src, dst;

        let i = length;
        let dst = 0;
        let src = histSize - length;
        while( i > 0 ) {
            let histX[dst] = histX[src];
            let histY[dst] = histY[src];
            let src = src + 1;
            let dst = dst + 1;
            let i = i - 1;
        }

        return;

    }

    // retorna a posição da cabeça da cobra
    method int posX() {
        return posX;
    }
    method int posY() {
        return posY;
    }

    // retorna o comprimento da cobra
    method int getLength() {
        return length;
    }

    // retorna a direção da cobra
    method int getDir() {
        return dir;
    }

    // retorna a ultima direção da cobra
    method int getLastDir() {
        return lastdir;
    }

    // direciona a cobra
    method void setDir(int newdir) {
        let dir = newdir;
        return;
    }

    // copia a ultima direção
    method void rememberDir() {
        let lastdir = dir;
        return;
    }

    // verifica se podemos mover mais um espaço no
    // direção atual
    method boolean tryMove() {

        var SnakeGrid grid;
        let grid = game.getGrid();

        if( dir = 1 ) {
            // up
            if( posY = 0 ) {
                return false;
            }
            else {
                let posY = posY - 1;
            }
        }
        if( dir = 2 ) {
            // down
            if( posY = grid.sizeY() ) {
                return false;
            }
            else {
                let posY = posY + 1;
            }
        }
        if( dir = 3 ) {
            // left
            if( posX = 0 ) {
                return false;
            }
            else {
                let posX = posX - 1;
            }
        }
        if( dir = 4 ) {
            // right
            if( posX = grid.sizeX() ) {
                return false;
            }
            else {
                let posX = posX + 1;
            }
        }

        // verifica se a cobra bateu em si mesma
        if( grid.checkOccupied(posX, posY) ) {
            return false;
        }

        return true;

    }

    // faz a cobra crescer
    method void grow(int cycle) {

        // desenha a nova cabeça
        do drawHead(cycle);

        // a cobra ainda pode crescer?
        if( togrow > 0 ) {
            let togrow = togrow - 1;
            let length = length + 1;
        }
        else {
            do clearTail(cycle);
        }

        return;

    }

    method void drawHead(int cycle) {

        var SnakeGrid grid;

        let grid = game.getGrid();
        do grid.drawSnakeBit(posX, posY);
        let histX[cycle] = posX;
        let histY[cycle] = posY;

        return;

    }

    method void clearTail(int cycle) {

        var SnakeGrid grid;
        var int tailIndex, tailX, tailY;

        let tailIndex = cycle - length;
        let tailX = histX[tailIndex];
        let tailY = histY[tailIndex];
        let grid = game.getGrid();
        do grid.clearSnakeBit(tailX, tailY);

        return;

    }

    // a cobra come uma bola
    method void eatFood() {
        let togrow = togrow + growby;
        return;
    }

}